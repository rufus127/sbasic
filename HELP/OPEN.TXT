OPEN nom-fich AS exp-num-1 [LEN exp-num-2]
Ouvre un fichier à accès direct sur disque et lui affecte un canal d’entrée-sortie.
nom-fich : Nom du fichier.
exp-num-1 : Numéro de canal compris entre 1 et 12.
exp-num-2 : Longueur de l’enregistrement logique.

Exemple 1
10 OPEN "VIRT" AS 1
20 DIM #1 A(10)
30 FOR X% = 0 TO 10
40 PRINT A(X%)
50 NEXT X%
60 CLOSE 1
Ce programme commence par ouvrir le fichier « VIRT.DAT » (ligne 10) et l’affecte au canal 1. Ce canal est ensuite associé au tableau virtuel de réels A(*), dont tous les éléments sont affichés (lignes 30 à 50).

Exemple 2
10 OPEN "PERSON" AS 2 LEN 50
20 FIELD #2, 30 AS NOM$, 12 AS PRE$, 8 AS SAL$
30 FOR X% = 1 TO 10
40 GET #2 RECORD X%
50 PRINT NOM$, PRE$, CVT$F(SAL$)
60 NEXT X%
70 CLOSE 2
Ce programme ouvre le fichier à accès direct « PERSON.DAT » et l’affecte au canal 2 en spécifiant une longueur d’enregistrement de 50 caractères. Après la définition du tampon d’entrée-sortie (ligne 20), les dix premiers enregistrements du fichier sont lus et les trois zones qui les composent sont affichées (lignes 30 à 60) ; enfin, le fichier est fermé (ligne 70).

Action
L’instruction OPEN effectue l’ouverture du fichier à accès direct nom-fich et l’affecte au canal exp-num-1 qui doit être compris entre 1 et 12.
* Le nom du fichier doit respecter les règles du système d’exploitation.
* Le cas échéant, la longueur de l’enregistrement est précisée par exp-num-2. La valeur par défaut de exp-num-2 est 252.
* Si le fichier n’existe pas, il est automatiquement créé.
Voir aussi : CLOSE.


OPEN {NEW | OLD | APPEND} nom-fich AS exp-num
Ouvre un fichier séquentiel sur disque en écriture, en lecture ou en extension et lui affecte un numéro de canal.
nom-fich : Nom d’un fichier séquentiel.
exp-num : Numéro de canal compris entre 1 et 12.

Exemple 1
10 OPEN NEW "CARRE" AS 1
20 FOR I% = 1 TO 6
30 PRINT #1 I%*I%
40 NEXT I%
50 CLOSE 1
Ce programme ouvre un nouveau fichier séquentiel « CARRE.DAT » (ligne 10) sur le canal 1 puis y écrit les carrés des 6 premiers nombres entiers.

Exemple 2
10 OPEN OLD "CARRE" AS 1
20 FOR I% = 1 TO 6
30 INPUT #1 C%
40 PRINT C%
50 NEXT I%
60 CLOSE 1
Ce programme ouvre le fichier séquentiel « CARRE.DAT » en lecture (ligne 10) puis lit son contenu, à savoir les 6 premiers carrés (voir exemple 1).

Exemple 3
10 OPEN APPEND "CARRE" AS 1
20 FOR I% = 7 TO 12
30 PRINT #1 I%*I%
40 NEXT I%
50 CLOSE 1
Ce programme ouvre le fichier séquentiel « CARRE.DAT » en extension (ligne 10) puis y ajoute les carrés des nombres 7 à 12 à la suite des carrés des 6 premiers (voir exemple 1).

Action
* L’instruction OPEN NEW ouvre le fichier séquentiel nom-fich en écriture et lui affecte le canal exp-num qui doit être compris entre 1 et 12.
Le fichier spécifié est forcément un nouveau fichier ; si un fichier de même nom existe déjà sur le disque, il est automatiquement détruit.
* L’instruction OPEN OLD ouvre le fichier séquentiel nom-fich en lecture du fichier séquentiel nom-fich et lui affecte le canal exp-num qui doit être compris entre 1 et 12.
Si le fichier spécifié n’existe pas sur le disque, une erreur 4 est produite.
* L’instruction OPEN APPEND ouvre le fichier séquentiel nom-fich en écriture et prépare l’enregistrement des prochaines écritures à la fin du fichier afin de l’étendre. Si le fichier spécifié n’existe pas sur le disque, il est automatiquement créé.

Le nom du fichier doit respecter les règles du système d’exploitation.

Voir aussi : CLOSE.


OPEN LIBRARY nom-fich AS exp-num
Ouvre une librairie à chargement dynamique (DLL) et lui affecte un canal d’entrée-sortie.
nom fich : Nom du fichier contenant la librairie.
exp-num  : Numéro de canal compris entre 1 et 32.

Exemple
10 OPEN LIBRARY "MaDll" AS 10
20 CALL #10 "MaFonction"(X%)
30 PRINT X%
40 CLOSE 10
Ce programme commence par ouvrir la librairie « MaDll.DLL » et l’affecte au canal 10. La fonction « MaFonction » de la librairie « MaDll.DLL » est ensuite appelée avec la variable entière X% en paramètre. La librairie est finalement fermée.

Action
L’instruction OPEN LIBRARY effectue l’ouverture de la librairie nom fich et l’affecte au canal exp-num qui doit être compris entre 1 et 32.
Le nom du fichier doit respecter les règles du système d’exploitation. Si l’extension n’est pas spécifiée, l’extension « .DLL » est ajoutée.
Le fichier qui contient la librairie doit être placé dans un répertoire exploré par le système d’exploitation. Sous Windows, le plus simple est de placer ce fichier dans le répertoire où se trouve le fichier « SBASIC.EXE ».
Voir aussi : CALL#, CLOSE.