  110 NE%=10
  120 DIM BB(NE%)
  130 FOR X% = 1 TO NE%
  140 BB(X%) = X%
  160 NEXT X%
  170 CALL SOMM (BB(*),NE%,SOM)
  175 PRINT "Somme:";SOM
  180 IF SOM <> 55 THEN PRINT "Erreur CALL!": STOP
  190 GOTO 300
  210 SUB SOMM (A(*),N%,S)
  220 S = 0
  230 FOR X% = 1 TO N%
  240 S = S + A(X%)
  250 NEXT X%
  260 RETURN
  300 I%=0: Y=0
  310 FOR X = 10 TO 1 STEP -0.5
  320 I%=I%+1
  330 Y=Y+X
  340 NEXT X
  345 PRINT "Boucles:";I%, "Sigma:";Y
  350 IF I% <> 19 OR Y <> 104.5 THEN "Erreur FOR!": STOP
  410 RAYON = 10: GOSUB 450
  415 IF CIRCONF <> 2*PI*10 THEN "Erreur GOSUB!": STOP
  420 RAYON = 20: GOSUB 450
  425 IF CIRCONF <> 2*PI*20 THEN "Erreur GOSUB!": STOP
  430 GOTO 510
  449 REM CALCUL DE LA CIRCONFERENCE D'UN CERCLE
  450 CIRCONF = 2*PI*RAYON
  460 PRINT "RAYON: "; RAYON,"CIRCONFERENCE: ";CIRCONF
  470 RETURN
  510 RAYON = 10: GOSUB CERCLE
  515 IF CIRCONF <> 2*PI*10 THEN "Erreur GOSUB LABEL!": STOP
  520 RAYON = 20: GOSUB CERCLE
  525 IF CIRCONF <> 2*PI*20 THEN "Erreur GOSUB LABEL!": STOP
  530 GOTO 600
  549 REM CALCUL DE LA CIRCONFERENCE D'UN CERCLE
  550 LABEL CERCLE: CIRCONF = 2*PI*RAYON
  560 PRINT "RAYON: "; RAYON,"CIRCONFERENCE: ";CIRCONF
  570 RETURN
  600 Z%=20: B%=10
  700 IF Z%>B% GOTO 1000
  800 PRINT "Erreur IF GOTO n!"
  900 STOP
  950 GOTO 1100
 1000 PRINT Z%;"EST SUPERIEUR A";B%
 1010 IF Z%<B% GOTO 800: PRINT "Erreur IF GOTO!": STOP
 1020 GOTO 950
 1030 PRINT "Erreur GOTO n arriere!": STOP
 1100 INPUT "VOULEZ-VOUS CONTINUER O/N "; A$
 1110 IF A$="O" OR A$="o" GOTO SUITE
 1120 PRINT "ARRET DU PROGRAMME"
 1130 END
 1140 LABEL SUITE
 1150 PRINT"SUITE DU PROGRAMME"
 1160 PRINT "Entrer la commande CONT pour continuer"
 1170 STOP
 1210 A% = 10 : CALL ESSAI
 1220 IF A% <> 10 THEN PRINT "Erreur LOCAL!": STOP
 1230 GOTO SUITE2
 1240 SUB ESSAI: LOCAL A%
 1250 FOR A% = 1 TO 3
 1260 PRINT A%
 1270 NEXT A%
 1280 RETURN
 1290 LABEL SUITE2
 1310 I%=0
 1320 I%=I%+1
 1330 PRINT I%;
 1340 IF I%<10 GOTO 1320
 1350 PRINT
 1360 IF I% <> 10 THEN PRINT "Erreur IF GOTO n arriere!": STOP
 1410 I%=0
 1415 LABEL BOUCLE
 1420 I%=I%+1
 1430 PRINT I%;
 1440 IF I%<10 GOTO BOUCLE
 1450 PRINT
 1460 IF I% <> 10 THEN PRINT "Erreur IF GOTO label arriere!": STOP
 1510 FOR A%=1 TO 5
 1520 ON A% GOSUB 1600,1610,1620,1630,1640
 1525 IF I% <> A% THEN PRINT "Erreur ON GOSUB!": STOP
 1527 PRINT I%;
 1530 NEXT A%
 1535 PRINT
 1540 GOTO 1650
 1600 I%=1: RETURN
 1610 I%=2: RETURN
 1620 I%=3: RETURN
 1630 I%=4: RETURN
 1640 I%=5: RETURN
 1650 REM commentaire
 1700 FOR A%=1 TO 6
 1720 ON A% GOTO 1790,DEUX,1890,QUATRE,1820,1830
 1730 PRINT "Erreur ON GOTO!": STOP
 1790 I%=1: GOTO 1970
 1820 I%=5: GOTO 1970
 1830 I%=6: GOTO 1970
 1890 I%=3: GOTO 1970
 1940 LABEL DEUX : I%=2: GOTO SUITE3
 1950 LABEL QUATRE : I%=4: GOTO SUITE3
 1960 PRINT "Erreur ON GOSUB!":STOP
 1970 LABEL SUITE3: PRINT I%;
 1980 IF I% <> A% GOTO 1960
 1990 NEXT A%
 1995 PRINT
 2000 ON INT 7 GOTO 2200
 2010 N=0 : BRK = 0
 2090 PRINT "APPUYEZ SUR CTRL C"
 2100 N=N+1: IF BRK THEN GOTO 2250 ELSE GOTO 2100
 2200 PRINT N;"BOUCLES EXECUTEES": BRK=1
 2210 RETURN
 2250 ON INT 7
 2310 OVERLAY 50
 3000 EXECUTE "3100 I=1: PRINT S$"
 3010 EXECUTE "3110 RETURN"
 3020 S$="GOSUB 3100"
 3025 I=0
 3030 EXECUTE S$
 3040 IF (I <> 1) THEN PRINT "Erreur EXECUTE!": STOP
 3050 DELETE 3100,3110
 3200 REM
 3210 TRON PRINT "A=";A;"B=";B; : C=C+1 : PRINT "Trace:";C
 3220 INPUT " A ET B"; A,B
 3230 A=A*2: B=B*3
 3240 A=A+50: B=B+120
 3250 TROFF
 4000 CALL CHKARG(0.1, 1./10, X, Y)
 4010 IF X <> 0.1 OR Y <> 1./10 THEN PRINT "Erreur argument CALL!": STOP
 4100 X=2:Y=3
 4110 CALL CHKARG(X, Y, X1, Y1)
 4120 IF X1 <> 2 OR Y1 <> 3 THEN PRINT "Erreur argument CALL!" :STOP
 4200 CALL CHKARGI (1, 2, X%, Y%)
 4210 IF X% <> 1 OR Y% <> 2 THEN PRINT "Erreur argument CALL!":STOP
 4300 X%=4:Y%=5
 4310 CALL CHKARGI(X%*1,Y%*2,X1%,Y1%)
 4320 IF X1% <> 4 OR Y1% <> 5*2 THEN PRINT "Erreur argument CALL!":STOP
 4410 CALL CHKARG(X%*1,Y%*2,X1,Y1)
 4420 IF X1 <> 4 OR Y1 <> 5*2 THEN PRINT "Erreur argument CALL!":STOP
 4430 DIM X%(2)
 4440 CALL CHKARGI(X*1,Y*2,X%(1),X%(2))
 4450 IF X%(1) <> 2 OR X%(2) <> 3*2 THEN PRINT "Erreur argument CALL!":STOP
 4490 GOTO 5000
 4500 SUB CHKARG (XA, YA, X, Y)
 4510 X=XA
 4520 Y=YA
 4530 RETURN
 4600 SUB CHKARGI(XA%, YA%, X%, Y%)
 4610 X%=XA%
 4620 Y%=YA%
 4630 RETURN
 5000 REM
22400 FOR I%=1 TO 10
22410 DIM EL(I%)
22420 FOR X% = 1 TO I%
22430 EL(X%) = 100 + X%
22440 NEXT X%
22450 FOR J%=1 TO 3
22499 REM *** Choix de la fonction ***
22550 ON J% GOTO 22620, 22630, 22640
22600 PRINT "Erreur ON GOTO!": STOP
22605 NEXT J%: CLEAR EL(*): NEXT I%: GOTO 22650
22610 REM *** Traitement de la fonction demand‚e ***
22620 BLOAD "SOMME",,10000 : CALL SOMME(I%,EL(*)) : GOTO 22605
22630 BLOAD "MOYEN",,10000 : CALL MOYEN(I%,EL(*)) : GOTO 22605
22640 BLOAD "ECART",,10000 : CALL ECART(I%,EL(*)) : GOTO 22605
22650 DELETE 10000,19999
30000 SETCOLOR 1,0,255,0: PRINT "Test OK"+CHR$(7)
